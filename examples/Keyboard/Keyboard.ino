/*
   Пример эмуляции клавиатуры EasyHID
   Можно нажимать, отжимать и кликать одновременно до 5ти клавиш
   Нажатые клавиши буфферизируются, клавиши модификаторы (ALT,CTRL...) так же работают
   Список всех доступных клавиш см. в файле Codekeys.h

*/

#include <EasyHID.h>					          // Либа HID 

static uint8_t count = 0;             // Направление
static uint32_t timer = millis();     // Миллис - таймер (НЕ DELAY!!!)
void setup() {
  HID.begin();                          // Инициализация USB
}

void loop() {
  if (millis() - timer >= 1000) {       // Каждые 3000 мс
    timer = millis();
    switch (count) {
      /* -------------------------------- Стандартные сценарии использования клавиатуры -------------------------------- */
      case 0:                                           // Пример #0 - печать текста
        Keyboard.println("");                           // Начнем с новой строки
        Keyboard.write('>');                            // Печать одного ascii символа
        Keyboard.print("Hello world ");                 // Стандартный print, можно печатать текст (ТОЛЬКО АНГЛ!) и численные значения
        break;

      case 1:                                           // Пример #1 - клик по обычным клавишам
        Keyboard.click(KEY_A);                          // Нажимаем и сразу отпускаем клавишу 'A'
        Keyboard.click(KEY_B, KEY_C);                   // Можно указывать вплоть до 5ти клавиш одновременно
        break;

      case 2:                                           // Пример #2 - нажатие клавиш, без отпускания
        Keyboard.press(KEY_1, KEY_2, KEY_3);            // Зажимаем одну или несколько клавиш
        break;

      case 3:                                           // Пример #3 - отпускание отдельной клавиши или нескольких клавиш
        Keyboard.release(KEY_2, KEY_3);                 // Пару отпустим, а '1' и дальше будет зажата
        break;

      case 4:                                           // Пример #4 - отпускание всех клавиш разом
        Keyboard.releaseAll();                          // Все ранее нажатые клавиши теперь отпущены
        break;

      case 5:                                           // Пример #6 - Сочетания клавиш, меняем язык в системе (SHIFT + ALT)
        Keyboard.click(KEY_LEFT_SHIFT, KEY_LEFT_ALT);   // Кликаем сочетание, можно и CTRL + ALT + DEL сделать!
        break;
        /* ---------------------------------------------------------------------------------------------------------------- */
    }
    if (++count > 5) count = 0;         // Циклический сдвиг от 0 до 3
  }
  HID.tick();                           // Вызываем тикер не реже чем каждые 10 мс!
}
